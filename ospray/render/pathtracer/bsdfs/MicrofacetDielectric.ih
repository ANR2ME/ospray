// ======================================================================== //
// Copyright 2009-2018 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use this file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "BSDF.ih"
#include "Fresnel.ih"
#include "GGXDistribution.ih"
#include "MicrofacetAlbedoTables.ih"

// Microfacet dielectric BSDF with the Smith microsurface model.
// [Walter et al., 2007, "Microfacet Models for Refraction through Rough Surfaces"]
// FIXME: improve robustness
struct MicrofacetDielectric
{
  BSDF super;

  float eta;
  GGXDistribution microfacet;
  float reflectionWeight;
};

inline BSDF_EvalRes MicrofacetDielectric_eval(const varying BSDF* uniform super,
                                              const vec3f& wo, const vec3f& wi, unsigned int& lcg)
{
  const varying MicrofacetDielectric* uniform self = (const varying MicrofacetDielectric* uniform)super;
  BSDF_EvalRes res;

  float cosThetaO = dot(wo, getN(super));
  if (cosThetaO <= 0.f)
    return make_BSDF_EvalRes_zero();
  float cosThetaI = dot(wi, getN(super));
  bool isReflection = cosThetaI > 0.f;

  // Compute the microfacet normal
  vec3f wh;

  if (isReflection)
  {
    // Reflection
    wh = wo + wi;
  }
  else
  {
    // Transmission
    wh = self->eta*wo + wi;
  }

  wh = normalize(wh);
  float cosThetaH = dot(wh, getN(super));
  if (cosThetaH < 0.f)
  {
    wh = neg(wh);
    cosThetaH = -cosThetaH;
  }

  float cosThetaOH = dot(wo, wh);
  float cosThetaIH = dot(wi, wh);

  // Fresnel term
  float cosThetaTH; // positive
  float F = fresnelDielectricEx(cosThetaOH, cosThetaTH, self->eta) * self->reflectionWeight;

  float value;
  if (isReflection)
  {
    // Reflection
    res.pdf = F * rcp(4.f*abs(cosThetaOH));
    value = F * rcp(4.f*cosThetaO);
  }
  else
  {
    // Transmission
    // cosThetaTH = -cosThetaIH
    res.pdf = (1.f-F) * cosThetaTH * rcp(sqr(self->eta*cosThetaOH - cosThetaTH));
    value = (1.f-F) * cosThetaTH * abs(cosThetaOH) * rcp(cosThetaO*sqr(self->eta*cosThetaOH - cosThetaTH)) * rcp(sqr(self->eta));
  }

  linear3f toLocal = transposed(getFrame(super));
  vec3f wo0 = toLocal * wo;
  vec3f wi0 = toLocal * wi;
  vec3f wh0 = toLocal * wh;

  float whPdf;
  float D = evalVisible(self->microfacet, wh0, wo0, cosThetaOH, whPdf);
  float G = evalG2(self->microfacet, wo0, wi0, cosThetaOH, cosThetaIH);

  res.pdf *= whPdf;
  res.value = make_vec3f(value * D * G);
  return res;
}

inline BSDF_SampleRes MicrofacetDielectric_sample(const varying BSDF* uniform super,
                                                  const vec3f& wo, const vec2f& s, float ss, unsigned int& lcg)
{
  const varying MicrofacetDielectric* uniform self = (const varying MicrofacetDielectric* uniform)super;
  BSDF_SampleRes res;

  float cosThetaO = dot(wo, getN(super));
  if (cosThetaO <= 0.f)
    return make_BSDF_SampleRes_zero();

  linear3f toGlobal = getFrame(super);
  linear3f toLocal = transposed(getFrame(super));
  vec3f wo0 = toLocal * wo;

  // Sample the microfacet normal
  float whPdf;
  vec3f wh = toGlobal * sampleVisibleUH(self->microfacet, wo0, whPdf, s);

  float cosThetaOH = dot(wo, wh);

  // Fresnel term
  float cosThetaTH; // positive
  float F = fresnelDielectricEx(cosThetaOH, cosThetaTH, self->eta) * self->reflectionWeight;

  // Sample the reflection or the transmission
  float cosThetaI;
  float weight;

  bool doReflection = (ss <= F);
  if (doReflection)
  {
    // Reflection
    res.wi = reflect(wo, wh, cosThetaOH);
    cosThetaI = dot(res.wi, getN(super));
    if (cosThetaI <= 0.f)
      return make_BSDF_SampleRes_zero();

    res.type = BSDF_GLOSSY_REFLECTION;
    res.pdf = F * rcp(4.f*abs(cosThetaOH));
    weight = 1.f;
  }
  else
  {
    // Transmission
    // cosThetaTH = -cosThetaIH
    res.wi = refract(wo, wh, cosThetaOH, cosThetaTH, self->eta);
    cosThetaI = dot(res.wi, getN(super));
    if (cosThetaI >= 0.f)
      return make_BSDF_SampleRes_zero();

    res.type = BSDF_GLOSSY_TRANSMISSION;
    res.pdf = (1.f-F) * cosThetaTH * rcp(sqr(self->eta*cosThetaOH - cosThetaTH));
    weight = rcp(sqr(self->eta));
  }

  float cosThetaIH = dot(res.wi, wh);
  vec3f wi0 = toLocal * res.wi;

  float G = evalG2(self->microfacet, wo0, wi0, cosThetaOH, cosThetaIH);

  res.pdf *= whPdf;
  res.weight = make_vec3f(weight * (G * rcp_safe(evalG1(self->microfacet, wo0, cosThetaOH))));
  return res;
}

inline void MicrofacetDielectric_Constructor(varying MicrofacetDielectric* uniform self,
                                             const varying linear3f* uniform frame,
                                             float eta, float roughness, float anisotropy,
                                             float reflectionWeight)
{
  BSDF_Constructor(&self->super, BSDF_GLOSSY,
                   MicrofacetDielectric_eval, MicrofacetDielectric_sample,
                   frame);

  self->eta = eta;
  self->microfacet = make_GGXDistribution(roughnessToAlpha(roughness, anisotropy));
  self->reflectionWeight = reflectionWeight;
}

inline varying BSDF* uniform MicrofacetDielectric_create(uniform ShadingContext* uniform ctx,
                                                         const varying linear3f* uniform frame,
                                                         float eta, float roughness, float anisotropy,
                                                         float reflectionWeight)
{
  varying MicrofacetDielectric* uniform self =
    (varying MicrofacetDielectric* uniform)ShadingContext_alloc(ctx, sizeof(MicrofacetDielectric));
  MicrofacetDielectric_Constructor(self, frame, eta, roughness, anisotropy, reflectionWeight);
  return &self->super;
}

// Helper function for transparent shadow rays
inline vec3f MicrofacetDielectric_getTransparency(float cosThetaO, float eta, float roughness, float anisotropy,
                                                  float reflectionWeight, unsigned int& lcg)
{
  float T = 1.f - MicrofacetDielectricReflectionAlbedoTable_eval(cosThetaO, eta, roughness) * reflectionWeight;
  return make_vec3f(T);
}
