// ======================================================================== //
// Copyright 2009-2018 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use this file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "MicrofacetDielectric.ih"

// Simplified Weidlich-Wilkie microfacet coating BSDF with the Smith microsurface model.
// Refraction for all rays and Fresnel for exiting rays are ignored, but absorption is
// computed from the refracted ray lengths. The advantages are that it does not suffer from
// energy loss, absorption behaves the same as in the original approach, and it's faster.
// [Weidlich and Wilkie, 2007, "Arbitrarily Layered Micro-Facet Surfaces"]
// [Walter et al., 2007, "Microfacet Models for Refraction through Rough Surfaces"]
struct MicrofacetDielectricLayer
{
  BSDF super;

  varying BSDF* varying substrate;
  bool fromOutside;
  float eta;
  vec3f transmittance;
  float thickness;
  GGXDistribution microfacet;
  float weight;
};

inline BSDF_EvalRes MicrofacetDielectricLayer_eval(const varying BSDF* uniform super,
                                                   const vec3f& wo, const vec3f& wi, unsigned int& lcg)
{
  const varying MicrofacetDielectricLayer* uniform self = (const varying MicrofacetDielectricLayer* uniform)super;

  float cosThetaO = dot(wo, getN(super));
  if (cosThetaO <= 0.f)
    return make_BSDF_EvalRes_zero();
  float cosThetaI = dot(wi, getN(super));

  // Evaluate the substrate
  // Ignore refraction
  BSDF_EvalRes substrate;
  foreach_unique (f in self->substrate)
    substrate = f->eval(f, wo, wi, lcg);

  // Apply the coating medium transmittance
  // Computing the path length from the original angles would result in too much absorption, so instead use the refracted angles
  float lengthO1 = rcp(refract(cosThetaO, self->eta)); // rcp(cosThetaO1)
  float lengthI1 = (cosThetaI > 0.f && self->fromOutside) ? rcp(refract(cosThetaI, self->eta)) : 0.f; // rcp(cosThetaI1)
  substrate.value = lerp(self->weight, substrate.value, substrate.value * pow(self->transmittance, self->thickness * (lengthO1 + lengthI1)));

  // Compute the final substrate reflection/transmission
  // Ignore Fresnel for the exiting ray
  if (self->fromOutside)
  {
    float T = MicrofacetDielectric_transmittance(cosThetaO, self->eta, self->microfacet, self->weight, LCG_getFloat2(lcg));
    substrate.value = substrate.value * T;
  }

  float coatingPickProb = self->fromOutside ? fresnelDielectricWeighted(cosThetaO, self->eta, self->weight) : 0.f; // have to use the macronormal for sampling
  float substratePickProb = 1.f - coatingPickProb;

  if (cosThetaI > 0.f && self->fromOutside)
  {
    // Compute the microfacet normal
    vec3f wh = normalize(wo + wi);
    float cosThetaOH = dot(wo, wh);

    // Fresnel term
    float F = fresnelDielectricWeighted(cosThetaOH, self->eta, self->weight);

    // Evaluate the coating reflection
    float cosThetaH = dot(wh, getN(super));
    float cosThetaIH = dot(wi, wh);

    linear3f toLocal = transposed(getFrame(super));
    vec3f wo0 = toLocal * wo;
    vec3f wi0 = toLocal * wi;
    vec3f wh0 = toLocal * wh;

    float whPdf;
    float D = evalVisible(self->microfacet, wh0, wo0, cosThetaOH, whPdf);
    float G = evalG2(self->microfacet, wo0, wi0, cosThetaOH, cosThetaIH);

    BSDF_EvalRes coating;
    coating.pdf = whPdf * rcp(4.f*cosThetaOH);
    coating.value = make_vec3f(F * D * G * rcp(4.f*cosThetaO));

    // Compute the total reflection
    BSDF_EvalRes res;
    res.pdf = coatingPickProb * coating.pdf + substratePickProb * substrate.pdf;
    res.value = coating.value + substrate.value;
    return res;
  }
  else
  {
    // Return the substrate transmission
    substrate.pdf *= substratePickProb;
    return substrate;
  }
}

inline BSDF_SampleRes MicrofacetDielectricLayer_sample(const varying BSDF* uniform super,
                                                       const vec3f& wo, const vec2f& s, float ss, unsigned int& lcg)
{
  const varying MicrofacetDielectricLayer* uniform self = (const varying MicrofacetDielectricLayer* uniform)super;
  BSDF_SampleRes res;

  float cosThetaO = dot(wo, getN(super));
  if (cosThetaO <= 0.f)
    return make_BSDF_SampleRes_zero();

  linear3f toGlobal = getFrame(super);
  linear3f toLocal = transposed(getFrame(super));
  vec3f wo0 = toLocal * wo;

  // Sample the coating or the substrate
  vec3f wh;
  BSDF_EvalRes substrate;

  float coatingPickProb = self->fromOutside ? fresnelDielectricWeighted(cosThetaO, self->eta, self->weight) : 0.f; // only the macronormal is available yet
  float substratePickProb = 1.f - coatingPickProb;

  if (ss < coatingPickProb)
  {
    // Sample the microfacet normal
    float whPdf;
    wh = toGlobal * sampleVisibleUH(self->microfacet, wo0, whPdf, s);
    float cosThetaOH = dot(wo, wh);

    res.type = BSDF_GLOSSY_REFLECTION;
    res.wi = reflect(wo, wh, cosThetaOH);

    // Evaluate the substrate
    // Ignore refraction
    foreach_unique (f in self->substrate)
      substrate = f->eval(f, wo, res.wi, lcg);
  }
  else
  {
    // Sample the substrate
    // Ignore refraction
    float ss1 = (ss - coatingPickProb) * rcp(substratePickProb); // reallocate sample
    foreach_unique (f in self->substrate)
      res = f->sample(f, wo, s, ss1, lcg);
    if (reduce_max(res.weight) <= 0.f)
      return res;
    substrate.pdf = res.pdf;
    substrate.value = res.weight * ((res.type & BSDF_SPECULAR) ? 1.f : res.pdf); // correctly handle delta distributions

    // Compute the microfacet normal
    wh = normalize(wo + res.wi);
  }

  float cosThetaI = dot(res.wi, getN(super));
  float cosThetaOH = dot(wo, wh);

  // Apply the coating medium transmittance
  // Computing the path length from the original angles would result in too much absorption, so instead use the refracted angles
  float lengthO1 = rcp(refract(cosThetaO, self->eta)); // rcp(cosThetaO1)
  float lengthI1 = (cosThetaI > 0.f && self->fromOutside) ? rcp(refract(cosThetaI, self->eta)) : 0.f; // rcp(cosThetaI1)
  substrate.value = lerp(self->weight, substrate.value, substrate.value * pow(self->transmittance, self->thickness * (lengthO1 + lengthI1)));

  // Compute the final substrate reflection/transmission
  // Ignore Fresnel for the exiting ray
  if (self->fromOutside)
  {
    float T = MicrofacetDielectric_transmittance(cosThetaO, self->eta, self->microfacet, self->weight, LCG_getFloat2(lcg));
    substrate.value = substrate.value * T;
  }

  if (res.type & BSDF_SPECULAR)
  {
    // If we sampled a delta distribution, we don't have to evaluate the coating reflection (see MultiBSDF_sample)
    res.weight = substrate.value * rcp(substratePickProb);
    return res;
  }
  else if (cosThetaI <= 0.f || !self->fromOutside)
  {
    // If we sampled transmission, we just have to return the substrate transmission
    res.pdf = substratePickProb * substrate.pdf;
    res.weight = substrate.value * rcp(res.pdf);
    return res;
  }
  else
  {
    // Fresnel term
    float F = fresnelDielectricWeighted(cosThetaOH, self->eta, self->weight);

    // Evaluate the coating reflection
    float cosThetaH = dot(wh, getN(super));
    float cosThetaIH = dot(res.wi, wh);
    vec3f wi0 = toLocal * res.wi;
    vec3f wh0 = toLocal * wh;

    float whPdf;
    float D = evalVisible(self->microfacet, wh0, wo0, cosThetaOH, whPdf);
    float G = evalG2(self->microfacet, wo0, wi0, cosThetaOH, cosThetaIH);

    BSDF_EvalRes coating;
    coating.pdf = whPdf * rcp(4.f*cosThetaOH);
    coating.value = make_vec3f(F * D * G * rcp(4.f*cosThetaO));

    // Compute the total reflection
    res.pdf = coatingPickProb * coating.pdf + substratePickProb * substrate.pdf;
    res.weight = (coating.value + substrate.value) * rcp(res.pdf);
    return res;
  }
}

inline void MicrofacetDielectricLayer_Constructor(varying MicrofacetDielectricLayer* uniform self, const varying linear3f* uniform frame,
                                                  varying BSDF* varying substrate, float eta, vec3f transmittance, float thickness, float roughness,
                                                  float weight)
{
  BSDF_Constructor(&self->super, BSDF_GLOSSY_REFLECTION | substrate->type,
                   MicrofacetDielectricLayer_eval, MicrofacetDielectricLayer_sample,
                   frame);

  self->substrate = substrate;
  self->fromOutside = eta < 1.f;
  self->eta = self->fromOutside ? eta : rcp(eta);
  self->transmittance = transmittance;
  self->thickness = thickness;
  self->microfacet = make_GGXDistribution(make_vec2f(roughnessToAlpha(roughness)));
  self->weight = weight;
}

inline varying BSDF* uniform MicrofacetDielectricLayer_create(uniform ShadingContext* uniform ctx, const varying linear3f* uniform frame,
                                                              varying BSDF* varying substrate, float eta, vec3f transmittance, float thickness, float roughness,
                                                              float weight)
{
  varying MicrofacetDielectricLayer* uniform self = (varying MicrofacetDielectricLayer* uniform)ShadingContext_alloc(ctx, sizeof(MicrofacetDielectricLayer));
  MicrofacetDielectricLayer_Constructor(self, frame, substrate, eta, transmittance, thickness, roughness, weight);
  return &self->super;
}

// Helper function for transparent shadow rays
inline vec3f MicrofacetDielectricLayer_getTransparency(float cosThetaO, float eta, vec3f transmittance, float thickness, float roughness,
                                                       float weight, unsigned int& lcg)
{
  GGXDistribution microfacet = make_GGXDistribution(make_vec2f(roughnessToAlpha(roughness)));

  bool fromOutside = eta < 1.f;
  if (!fromOutside) eta = rcp(eta);

  // Apply the coating medium transmittance
  // Computing the path length from the original angles would result in too much absorption, so instead use the refracted angles
  float lengthO1 = rcp(refract(cosThetaO, eta)); // rcp(cosThetaO1)
  vec3f value = lerp(weight, make_vec3f(1.f), pow(transmittance, thickness * lengthO1));

  // Compute the final transmittance
  if (fromOutside)
  {
    float T = MicrofacetDielectric_transmittance(cosThetaO, eta, microfacet, weight, LCG_getFloat2(lcg));
    value = value * T;
  }
  return value;
}
