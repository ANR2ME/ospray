// ======================================================================== //
// Copyright 2009-2016 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use this file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "../lights/PTLight.ih"
#include "patterns.ih"

#define MAX_SAMPLES_1D 21
#define MAX_SAMPLES_2D 21
#define MAX_SAMPLES_LIGHT 5

/*! A complete high-dimensional sample, attached to one sample in the image plane. */
struct PrecomputedSample 
{
  varying vec2f pixel;               //!< Sample location inside the pixel. [0.5;0.5] is the pixel center.
  varying float time;                //!< time sample for motion blur.
  varying vec2f lens;                //!< 2D lens sample for depth of field.
  varying float samples1D[MAX_SAMPLES_1D];        //!< Additional 1D samples requested by the integrator.
  varying vec2f samples2D[MAX_SAMPLES_2D];        //!< Additional 2D samples requested by the integrator.
  varying PTLightSample lightSamples[MAX_SAMPLES_LIGHT]; //!< Precomputed light samples.
};

/*! Pixel sample position. */
inline const varying vec2f PrecomputedSample__getPixel(const uniform PrecomputedSample* uniform self) { 
  return self->pixel; 
}

/*! Get the current lens sample. */
inline const varying vec2f PrecomputedSample__getLens(const uniform PrecomputedSample* uniform self) { 
  return self->lens; 
}

/*! Get the current time sample. */
inline const varying float PrecomputedSample__getTime(const uniform PrecomputedSample* uniform self) { 
  return self->time; 
}

/*! Get the specified additional 1D sample for the current sample. */
inline const varying float PrecomputedSample__getFloat (const uniform PrecomputedSample* uniform self, uniform int dim) { 
  return self->samples1D[dim]; 
}

/*! Get the specified additional 2D sample for the current sample. */
inline const varying vec2f PrecomputedSample__getVec2f(const uniform PrecomputedSample* uniform self, uniform int dim) { 
  return self->samples2D[dim]; 
}

/*! Get the specified precomputed light sample for the current sample. */
inline const varying PTLightSample PrecomputedSample__getLightSample(const uniform PrecomputedSample* uniform self, uniform int lightSampleId) {
  return self->lightSamples[lightSampleId]; 
}

/*! The sampler factory precomputes samples for usage by multiple sampler threads. */
struct PrecomputedSampler 
{
  uint32 numSamples1D;                  //!< Number of additional 1D samples per pixel sample.
  uint32 numSamples2D;                  //!< Number of additional 2D samples per pixel sample.
  uint32 numLightSamples;               //!< Number of precomputed light samples per pixel sample.
  uniform PTLight* lights[MAX_SAMPLES_LIGHT];        //!< References to all light sources.
  uniform int32 lightBaseSamples[MAX_SAMPLES_LIGHT]; //!< Base samples for light sample precomputation.
  
  uint32 samplesPerPixel;               //!< Number of samples per pixel.
  uint32 sampleSets;                    //!< Number of precomputed sample sets.
  uint32 totalSamplesMask;              //!< sampleSets*samplesPerPixel-1
  uniform PrecomputedSample* uniform samples; //!< All precomputed samples.
  uniform PrecomputedSample* uniform _samples; //!< All precomputed samples.
};

inline void PrecomputedSampler__Destructor(uniform PrecomputedSampler* uniform self) {
  LOG(print("PrecomputedSampler__Destructor\n"));
  delete[] self->_samples;
}

/*! Construction from parameters. */
inline void PrecomputedSampler__Constructor(uniform PrecomputedSampler* uniform self,
                                            const uniform int samplesPerPixel,
                                            const uniform int sampleSets)
{
  LOG(print("PrecomputedSampler__Constructor\n"));
  self->numSamples1D = 0;
  self->numSamples2D = 0;
  self->numLightSamples = 0;
  self->samplesPerPixel = samplesPerPixel;
  self->sampleSets = sampleSets;
  self->totalSamplesMask = samplesPerPixel*sampleSets-1;
  self->_samples = NULL;
  self->samples = NULL;
}
    
/*! Request additional 1D samples per pixel sample. */
inline uniform int PrecomputedSampler__request1D(uniform PrecomputedSampler* uniform self, uniform int num) 
{
  uniform int dim = self->numSamples1D;
  self->numSamples1D += num;
  if (self->numSamples1D > MAX_SAMPLES_1D) print("ERROR: Too many 1D samples requested.\n");
  return dim;
}

/*! Request additional 2D samples per pixel sample. */
inline uniform int PrecomputedSampler__request2D(uniform PrecomputedSampler* uniform self, uniform int num) 
{
  uniform int dim = self->numSamples2D;
  self->numSamples2D += num;
  if (self->numSamples2D > MAX_SAMPLES_2D) print("ERROR: Too many 2D samples requested.\n");
  return dim;
}

/*! Request a precomputed light sample. */
inline uniform int PrecomputedSampler__requestLightSample(uniform PrecomputedSampler* uniform self, uniform int baseSample, uniform PTLight* uniform light)
{
  self->lights[self->numLightSamples] = light;
  self->lightBaseSamples[self->numLightSamples] = baseSample;
  self->numLightSamples++;
  if (self->numLightSamples > MAX_SAMPLES_LIGHT) print("ERROR: Too many light samples requested.\n");
  return self->numLightSamples-1;
}

inline uniform PrecomputedSample* uniform 
PrecomputedSampler__get(const uniform PrecomputedSampler* uniform self, 
                        uniform uint32 i, uniform uint32 s) 
{
  return &self->samples[(i*self->samplesPerPixel+s)&self->totalSamplesMask];
}

/*! Initialize the factory for a given iteration and precompute all samples. */
void PrecomputedSampler__init(uniform PrecomputedSampler* uniform self)
{
  print("Generating % MB of precalculated samples  ",
        (self->sampleSets*self->samplesPerPixel*sizeof(uniform PrecomputedSample))/(1024*1024));
  
  //varying Random rng; rng.Constructor(1243);
  varying Random rng; Random__Constructor(&rng, 1243 + 5464*programIndex);
  self->_samples = uniform new uniform PrecomputedSample[self->sampleSets*self->samplesPerPixel+1];
  self->samples = (uniform PrecomputedSample* uniform) align_ptr(self->_samples);

  varying vec2f* uniform _pixel = uniform new varying vec2f[self->samplesPerPixel+1];
  varying float* uniform _time = uniform new varying float[self->samplesPerPixel+1];
  varying vec2f* uniform _lens = uniform new varying vec2f[self->samplesPerPixel+1];
  varying float* uniform _samples1D = uniform new varying float[self->samplesPerPixel+1];
  varying vec2f* uniform _samples2D = uniform new varying vec2f[self->samplesPerPixel+1];

  varying vec2f* uniform pixel = (varying vec2f* uniform) align_ptr(_pixel); 
  varying float* uniform time = (varying float* uniform) align_ptr(_time);
  varying vec2f* uniform lens = (varying vec2f* uniform) align_ptr(_lens);
  varying float* uniform samples1D = (varying float* uniform) align_ptr(_samples1D);
  varying vec2f* uniform samples2D = (varying vec2f* uniform) align_ptr(_samples2D);

  for (uniform int set = 0; set < self->sampleSets; set++)
  {
    print(".");
    
    /*! Generate pixel and lens samples. */
    multiJittered(pixel, self->samplesPerPixel, rng);
    jittered(time, self->samplesPerPixel, rng);
    multiJittered(lens, self->samplesPerPixel, rng);
    
    for (uniform int s = 0; s < self->samplesPerPixel; s++) 
    {
      self->samples[set*self->samplesPerPixel+s].pixel = pixel[s];
      self->samples[set*self->samplesPerPixel+s].time  = time[s];
      self->samples[set*self->samplesPerPixel+s].lens  = lens[s];
    }

    /*! Generate requested 1D samples. */
    for (uniform int d = 0; d < self->numSamples1D; d++) {
      jittered(samples1D, self->samplesPerPixel, rng);
      for (uniform int s = 0; s < self->samplesPerPixel; s++) {
        self->samples[set*self->samplesPerPixel+s].samples1D[d] = samples1D[s];
      }
    }

    /*! Generate 2D samples. */
    for (uniform int d = 0; d < self->numSamples2D; d++) {
      multiJittered(samples2D, self->samplesPerPixel, rng);
      for (uniform int s = 0; s < self->samplesPerPixel; s++) {
        self->samples[set*self->samplesPerPixel+s].samples2D[d] = samples2D[s];
      }
    }

    /*! Generate light samples. */
    for (uniform int d = 0; d < self->numLightSamples; d++) {
      for (uniform int s = 0; s < self->samplesPerPixel; s++) {
        varying PTLightSample ls;
        varying DifferentialGeometry dg; 
        varying vec2f sample = self->samples[set*self->samplesPerPixel+s].samples2D[self->lightBaseSamples[d]];
        ls.L = self->lights[d]->sample(self->lights[d], dg, ls.wi, ls.tMax, sample);
        self->samples[set*self->samplesPerPixel+s].lightSamples[d] = ls;
      }
    }
  }
  delete[] _pixel;
  delete[] _time;
  delete[] _lens;
  delete[] _samples1D;
  delete[] _samples2D;
  print(" [DONE]\n");
}

/*! Reset the sampler factory. Delete all precomputed samples. */
inline void PrecomputedSampler__reset(uniform PrecomputedSampler* uniform self)
{
  delete[] self->_samples;
  self->_samples = NULL;
  self->samples = NULL;
  self->numSamples1D = 0;
  self->numSamples2D = 0;
  self->numLightSamples = 0;
}

inline bool PrecomputedSampler__empty (uniform PrecomputedSampler* uniform self) 
{
  return self->samples == NULL;
}
